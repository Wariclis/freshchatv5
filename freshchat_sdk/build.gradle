apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
group='com.github.freshdesk'

def static getEscapedString(value) {
    return "\"$value\"";
}

def getScmRevision = {
    String rev = 'git rev-parse --short HEAD'.execute().text.trim()
    return getEscapedString(rev)
}

def getScmBranch = {
    String branch = "git rev-parse --abbrev-ref HEAD".execute().text.trim()
    return getEscapedString(branch)
}

android {
    compileSdkVersion 26
    buildToolsVersion "28.0.3"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    defaultConfig {
        minSdkVersion 16
        versionCode 291
        versionName "2.9.1"
        targetSdkVersion 28
        consumerProguardFiles 'consumer-proguard-rules.pro'
        buildConfigField "long", 'BUILT_AT', System.currentTimeMillis() + 'L'
        buildConfigField "String", "SCM_REV", getScmRevision()
        buildConfigField "String", "SCM_BRANCH", getScmBranch()
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'sdk-proguard-rules.pro'
        }
    }
}

ext {
    gsonVersion             = '2.8.0'
    picassoVersion          = '2.5.2'
    supportLibraryVersion   = '28.0.0'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.google.code.gson:gson:$gsonVersion"
    compile "com.squareup.picasso:picasso:$picassoVersion"
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$supportLibraryVersion"
}
